plugins {
	id 'org.jetbrains.intellij.platform' version '2.5.0'
	id 'java'
}

group 'org.sireum.intellij.asm'
version '9.8'

repositories {
	mavenCentral()
	
	intellijPlatform {
		defaultRepositories()
	}
}

//setup a config so gradle prioritises our version of ASM
configurations {
	priority
	sourceSets.main.compileClasspath = configurations.priority + sourceSets.main.compileClasspath
	sourceSets.main.runtimeClasspath = configurations.priority + sourceSets.main.runtimeClasspath
}

dependencies {
	
	implementation group: 'org.ow2.asm', name: 'asm', version: asm_version
	
	priority "org.ow2.asm:asm:$asm_version"
	priority "org.ow2.asm:asm-commons:$asm_version"
	priority "org.ow2.asm:asm-util:$asm_version"
	
	//duplicated here so that IJ plugin puts them in the zip distribution
	runtimeOnly "org.ow2.asm:asm:$asm_version"
	runtimeOnly "org.ow2.asm:asm-commons:$asm_version"
	runtimeOnly "org.ow2.asm:asm-util:$asm_version"
	
	intellijPlatform {
		intellijIdeaCommunity "2024.3.5"
		bundledPlugin "com.intellij.java"
		
		pluginVerifier()
		instrumentationTools()
	}
}

java {
	targetCompatibility = JavaVersion.VERSION_21
}
